plugins {
    id 'idea'
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.15'
    id 'io.freefair.lombok' version '5.3.3.3'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group 'com.gotocompany'
version '0.7.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'
    api group: 'com.google.protobuf', name: 'protobuf-java', version: '3.23.2'
    api group: 'com.datadoghq', name: 'java-dogstatsd-client', version: '2.13.0'
    implementation group: 'org.json', name: 'json', version: '20230618'
    implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.18.0'
    testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '12.0-rc2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
    testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/test/generated"
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1"
    }
    generateProtoTasks {
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.includeSourceInfo = false
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.path = "$projectDir/src/test/resources/__files/descriptors.bin"
        }
    }
}

idea {
    module {
        testSourceDirs += file("$projectDir/src/generated/test/java")
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            pom {
                name = 'Stencil'
                description = 'Library to pull latest proto descriptors from a remote server'
                url = 'https://github.com/goto/stencil'

                scm {
                    url = 'https://github.com/goto/stencil.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'github-actions[bot]'
                        name = 'github-actions[bot]'
                        email = '41898282+github-actions[bot]@users.noreply.github.com'
                    }
                }
                from components.java
            }
        }
    }
}

signing {
   sign publishing.publications.maven
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("MAVEN_USERNAME")
            password = System.getenv("MAVEN_PASSWORD")
        }
    }
}
